plugins {
    id 'checkstyle'
}

test {
    // Pass on downloader type to tests for different CI jobs. See DownloaderFactory.java and ci.yml
    if (System.properties.containsKey('downloader')) {
        systemProperty('downloader', System.getProperty('downloader'))
    }
    useJUnitPlatform()
    dependsOn checkstyleMain // run checkstyle when testing
}

checkstyle {
    getConfigDirectory().set(rootProject.file("checkstyle"))
    ignoreFailures false
    showViolations true
    toolVersion checkstyleVersion
}

checkstyleTest {
    enabled false // do not checkstyle test files
}

dependencies {
    implementation project(':timeago-parser')

    implementation "com.github.TeamNewPipe:nanojson:$nanojsonVersion"
    implementation 'org.jsoup:jsoup:1.15.3'
    implementation "com.github.spotbugs:spotbugs-annotations:$spotbugsVersion"
    implementation 'org.nibor.autolink:autolink:0.10.0'

    // do not upgrade to 1.7.14, since in 1.7.14 Rhino uses the `SourceVersion` class, which is not
    // available on Android (even when using desugaring), and `NoClassDefFoundError` is thrown
    implementation 'org.mozilla:rhino:1.7.14'

    checkstyle "com.puppycrawl.tools:checkstyle:$checkstyleVersion"

    testImplementation platform("org.junit:junit-bom:$junitVersion")
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'

    testImplementation "com.squareup.okhttp3:okhttp:3.12.13"
    testImplementation 'com.google.code.gson:gson:2.9.1'
}
