allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    version 'v0.24.5'
    group 'com.github.TeamNewPipe'

    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
    }

    afterEvaluate {
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                }
            }
        }
    }

    ext {
        // When updating nanojson from FireMasterK's fork, take the last commit that does not use
        // fastutil, since we don't want to add fastutil as a dependency of NewPipeExtractor.
        nanojsonVersion = "f0493317e52614f296bcb491d7838b7c6e5823e6"
        jsr305Version = "3.0.2"
        junitVersion = "5.12.1"
        checkstyleVersion = "10.4"
    }
}

dependencies {
    api project(':extractor')
    implementation project(':timeago-parser')
}

subprojects {
    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    tasks.withType(Test) {
        testLogging {
            events "skipped", "failed"
            showStandardStreams = true
            exceptionFormat = 'full'
        }
    }

    artifacts {
        archives sourcesJar
    }
}

// https://discuss.gradle.org/t/best-approach-gradle-multi-module-project-generate-just-one-global-javadoc/18657/21
task aggregatedJavadocs(type: Javadoc, group: 'Documentation') {
    destinationDir = file("$buildDir/docs/javadoc")
    title = "$project.name $version"
    // options.memberLevel = JavadocMemberLevel.PRIVATE
    options.links 'https://docs.oracle.com/javase/8/docs/api/'
    options.encoding 'UTF-8'
    // Fixes unknown tag @implNote; the other two were added precautionary
    options.tags = [
            "apiNote:a:API Note:",
            "implSpec:a:Implementation Requirements:",
            "implNote:a:Implementation Note:"
    ]

    subprojects.each { project ->
        project.tasks.withType(Javadoc).each { javadocTask ->
            source += javadocTask.source
            classpath += javadocTask.classpath
            excludes += javadocTask.excludes
            includes += javadocTask.includes
        }
    }
}
